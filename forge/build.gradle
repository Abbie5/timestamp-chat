plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'maven-publish'
}

evaluationDependsOn(":xplat")

archivesBaseName = "${project.archives_base_name}-forge"

minecraft {
    mappings channel: 'official', version: project.minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            mods {
                timestamp_chat {
                    source sourceSets.main
                    source project(":xplat").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            mods {
                timestamp_chat {
                    source sourceSets.main
                    source project(":xplat").sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "net.minecraftforge:forge:${project.forge_version}"

    compileOnly project(":xplat")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}")
}

mixin {
    add sourceSets.main, "timestamp_chat.refmap.json"
    config "timestamp_chat.mixins.json"
}

jar.finalizedBy('reobfJar')

compileJava {
    source(project(":xplat").sourceSets.main.allSource)
}

processResources {
    from project(":xplat").sourceSets.main.resources
}

jar {
    manifest {
        attributes([
                "Implementation-Version"  : project.jar.archiveVersion,
        ])
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
